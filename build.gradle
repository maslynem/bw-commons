plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'ru.digitalyard.commons'
    repositories {
        mavenCentral()
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/maslynem/DigitalYard")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("githubUsername") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubToken") ?: ""
            }
        }
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '17'
        targetCompatibility = '17'
        withJavadocJar()
        withSourcesJar()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.2.0"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
        repositories {
            maven {
                name = "github"
                url = uri("https://maven.pkg.github.com/maslynem/DigitalYard")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("githubUsername") ?: ""
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubToken") ?: ""
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }
tasks.withType(PublishToMavenLocal).configureEach { it.enabled = false }
