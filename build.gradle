plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    group = 'ru.digitalyard.commons'
    version = '1.0.2-SNAPSHOT'
    repositories {
        mavenCentral()
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/maslynem/DigitalYard")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("githubUsername") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubToken") ?: ""
            }
        }
        mavenLocal()
    }
    tasks.withType(Javadoc).configureEach {
        enabled = false
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '17'
        targetCompatibility = '17'
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.0"
        }
    }

    publishing {
        publications {
            create("mavenJava", MavenPublication) {
                from(components.java)
                versionMapping {
                    usage("java-api") {
                        fromResolutionOf("runtimeClasspath")
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
                artifactId = project.name
            }
        }
        repositories {
            maven {
                name = "github"
                url = uri("https://maven.pkg.github.com/maslynem/DigitalYard")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("githubUsername") ?: ""
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubToken") ?: ""
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += '-parameters'
    }
}

tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }
tasks.withType(PublishToMavenLocal).configureEach { it.enabled = false }

plugins.withType(CheckstylePlugin).configureEach {
    checkstyle {
        skip = true
    }
}